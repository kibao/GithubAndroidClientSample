apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

repositories {
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"

    defaultConfig {
        applicationId "pl.kibao.githubclient"

        minSdkVersion 14
        targetSdkVersion 24

        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "pl.kibao.githubclient.TestRunner"
        // Data binding does not support Jacks builds yet :(
//        jackOptions {
//            enabled true
//        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "com.android.support:appcompat-v7:$rootProject.supportLibraryVersion"
    compile "com.android.support:design:$rootProject.supportLibraryVersion"
    compile "com.android.support:cardview-v7:$rootProject.supportLibraryVersion"

    compile 'com.google.dagger:dagger:2.7'
    apt 'com.google.dagger:dagger-compiler:2.7'

    compile 'com.google.code.gson:gson:2.7'
    compile 'io.reactivex:rxjava:1.2.1'
    compile 'io.reactivex:rxandroid:1.2.1'


    compile 'com.squareup.okhttp3:okhttp:3.4.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile 'com.jakewharton.timber:timber:4.3.1'

    compile 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    compile 'com.github.bumptech.glide:glide:3.7.0'

    testCompile 'junit:junit:4.12'

    androidTestCompile "com.android.support:support-annotations:$rootProject.supportLibraryVersion"
    androidTestCompile "com.android.support.test:runner:0.5"
    androidTestCompile "com.android.support.test:rules:0.5"
    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'
    androidTestCompile("com.android.support.test.espresso:espresso-core:2.2.2"){
        exclude module: 'support-annotations'
    }
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:2.2.2") {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }
    androidTestCompile 'com.android.support.test.espresso:espresso-intents:2.2.2'
    androidTestCompile "org.mockito:mockito-core:1.10.19"
    androidTestCompile 'com.github.fabioCollini:DaggerMock:0.6.2'

    // https://github.com/fabioCollini/DaggerMock/issues/16#issuecomment-226303370
    def dexmakerVersion = 'f8e7c23e4172af8767758c99c9165dd2bd4f0e7f'
    androidTestCompile "com.github.crittercism.dexmaker:dexmaker-parent:$dexmakerVersion"
    androidTestCompile "com.github.crittercism.dexmaker:dexmaker-mockito:$dexmakerVersion"
    androidTestCompile "com.github.crittercism.dexmaker:dexmaker-dx:$dexmakerVersion"
    androidTestCompile "com.github.crittercism.dexmaker:dexmaker:$dexmakerVersion"
}

/*
 * Workaround for https://code.google.com/p/android/issues/detail?id=182715
 *
 * The Android Gradle plugin is creating DataBindingExportBuildInfoTasks for the instrumentation
 * APK that generates from the app APKs layouts. This creates duplicate classes in the app and
 * instrumentation APK which leads to
 *      java.lang.IllegalAccessError: Class ref in pre-verified class resolved to unexpected implementation
 * on older devices.
 *
 * The workaround is to get the DataBindingExportBuildInfoTasks tasks for the instrumentation APK
 * and delete the files right after it creates them.
 */
tasks.withType(com.android.build.gradle.internal.tasks.databinding.DataBindingExportBuildInfoTask) { task ->
    if (task.name.endsWith("AndroidTest")) {
        task.finalizedBy(tasks.create("${task.name}Workaround") << {
            task.output.deleteDir()
        })
    }
}
